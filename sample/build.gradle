apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "net.sharewire.googlemapsclustering.sample"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName

        def secureProps = new Properties()
        if (file("../secure.properties").exists()) {
            file("../secure.properties")?.withInputStream { secureProps.load(it) }
        }
        resValue "string", "google_maps_key", (secureProps.getProperty("GOOGLE_MAPS_API_KEY") ?: "")

        // To add your Maps API key to this project:
        // 1. Create a file ApiDemos/java/secure.properties
        // 2. Add this line, where YOUR_API_KEY is your API key:
        //        GOOGLE_MAPS_API_KEY=YOUR_API_KEY
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        // The application doesn't have to be indexable by Google Search.
        disable 'GoogleAppIndexingWarning'
    }

    packagingOptions {
        exclude 'META-INF/proguard/androidx-annotations.pro'
        exclude 'META-INF/androidx.interpolator_interpolator.version'
    }
}

dependencies {
    implementation project(':library')
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "me.tatiyanupanwong.supasin.android.libraries.kits.maps:maps-core:$mapKitVersion"
    implementation "me.tatiyanupanwong.supasin.android.libraries.kits.maps:maps-google:1.2.2"
}
